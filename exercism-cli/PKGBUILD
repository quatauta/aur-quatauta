# Maintainer: linduxed <linduxed at gmail dot com>

pkgname=exercism-cli
pkgver=3.0.12
pkgrel=3
pkgdesc="Command line client for exercism.io"
arch=("i686" "x86_64")
url="https://github.com/exercism/cli"
license=("MIT")
source=("https://raw.githubusercontent.com/exercism/cli/v$pkgver/LICENSE"
        'http://cli.exercism.io/shell/exercism_completion.bash'
        'http://cli.exercism.io/shell/exercism_completion.zsh')
sha256sums=('5dd14ba854091ff4226fa6c7517b57da86acbe9b6a00c36f0c031dd4f4816c90'
            'feef8a47ba8bf25d8216189dbb9cbe91ceeb598443fab859004ee0f31ca78431'
            '00821f0b917b6e9b32edb811cdfcc4991bb305198e79feb7d349be96d69ed2c4')
sha256sums_i686=('b16f2918999ada0b5452055b69c45e7d93410cf3e1a40892142ab721368230d2')
sha256sums_x86_64=('9e212387f8256cf52039b2f6cad122ec7c8739f9fcd15ae57c908886953df0e3')
source_i686=(exercism-linux-32bit-v$pkgver.tgz::"https://github.com/exercism/cli/releases/download/v$pkgver/exercism-linux-32bit.tgz")
source_x86_64=(exercism-linux-64bit-v$pkgver.tgz::"https://github.com/exercism/cli/releases/download/v$pkgver/exercism-linux-64bit.tgz")

package(){
    cd "$srcdir"
    install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -D -m644 exercism_completion.bash "$pkgdir/usr/share/$pkgname/completion/exercism_completion.bash"
    install -D -m644 exercism_completion.zsh "$pkgdir/usr/share/$pkgname/completion/exercism_completion.zsh"
    install -D exercism "$pkgdir/usr/bin/exercism"
}
